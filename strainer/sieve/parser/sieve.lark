start: commands
commands: command*
command: command_name arguments block
command_name: CONTROL | ACTION | IDENTIFIER
block: ";" | "{" commands "}"
test: test_name arguments
test_name: TEST | IDENTIFIER
arguments: (string_list | string | NUMBER | tag)* [test | test_list]
tag: OVER_UNDER | COMPARATOR | ADDRESS_PART | MATCH_TYPE | BODY_TRANSFORM | TAG
_list{type}: type ("," type)*
test_list: "(" _list{test} ")"
string_list: "[" _list{string} "]"
string: STRING | TEXT

// Explicitly define to allow separate styling
ACTION: /keep|discard|fileinto|redirect/
CONTROL: /if\b|elsif\b|else|require|stop/
TEST: /true|false|not\b|allof|anyof|exists|size|header|address|envelope|body/
OVER_UNDER: /:over\b|:under\b/
COMPARATOR: ":comparator"
ADDRESS_PART: /:localpart|:domain|:all/
MATCH_TYPE: /:is|:contains|:matches/
BODY_TRANSFORM: /:raw|:content|:text/

// Explicitly defined because automatic name would be something like __ANON_0
IDENTIFIER.0: /[a-z_][a-z0-9_]*/
TAG.0: ":" IDENTIFIER
NUMBER: /[0-9]+[KMG]/

// Explicitly defined to allow ignoring of whitespace
STRING: "\"" /\r\n|[^\0\r\n"\\]|\\[^\0\r\n]/* "\""
TEXT: "text:" /[ \t]*/ /#[^\0\r\n]*/? "\r\n" (/[^\0\r\n.]|[^\0\r\n]{2,}/? "\r\n")* ".\r\n"
COMMENT: /#[^\0\r\n]*(?=\r\n|$)/ | "/*" /\r\n|\*(?!\/)|[^\0\r\n*]/* "*/"
WHITE_SPACE: /[ \t]|\r\n/
%ignore COMMENT
%ignore WHITE_SPACE
